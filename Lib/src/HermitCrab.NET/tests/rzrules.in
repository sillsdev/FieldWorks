;Test REALIZATIONAL RULES during analysis.
;First load some nat classes (msg 6541):
(load_nat_class message <nat_class name null features ()>)
(load_nat_class message <nat_class name C features (cons +)>)
(load_nat_class message <nat_class name V features (voc +)>)
(load_nat_class message <nat_class name alv_stop features (cons + strident - del_rel - poa alveolar)>)
(load_nat_class message <nat_class name voiceless_c features (cons + vd -)>)
(load_nat_class message <nat_class name labiodental features (cons + poa labiodental)>)
(load_nat_class message <nat_class name voiced features (vd +)>)
(load_nat_class message <nat_class name strident features (cons + strident +)>)
;Load two simple realizational rules (msg 6505, twice).
; English -ed suffix:
(load_morpher_rule message
  <rz_rule nm ed_suffix
		   str morphophonemic
		   rz_f (tense (past))
		   gl PAST
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class alv_stop>)
									  )
						  >
					   out <m_rhs p_out (1 2 (d table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>
										<simp_cntxt class voiceless_c>
									   )
									  )
						  >
					   out <m_rhs p_out (1 (t table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (d table3))>
					 >
					)
  >
)
;English -s suffix:
(load_morpher_rule message
  <rz_rule nm s_suffix
		   str morphophonemic
		   rz_f (pers (3) tense (pres))
		   gl 3SG
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class labiodental>)
									  )
						  >
					   out <m_rhs p_out (1 (2 <simp_cntxt class voiced>) (z table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>
										<simp_cntxt class strident>
									   )
									  )
						  >
					   out <m_rhs p_out (1 (z table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class voiceless_c>)
									  )
						  >
					   out <m_rhs p_out (1 2 (s table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (z table3))>
					 >
					)
	>
)
;The above two rules will belong to a single slot; the following rule will
; belong to a different slot:
(load_morpher_rule message
  <rz_rule nm evidential
		   str morphophonemic
		   rz_f (evidential (witnessed))
		   gl WIT
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (v table3))>
					 >
					)
	>
)

;Now define an inflectional template for the above rules:
(set_stratum message <stratum_setting nm morphophonemic type templates
					  value (<affix_template nm verb r_pos (V) slots ((s_suffix ed_suffix) (evidential))>)>)

;Turn on some tracing:
(trace_morpher_rule message (true true))
(trace_morpher_templates message (true true))
(trace_blocking message true)
(trace_lexical_lookup message true)
;Test, one suffix, expect one parse:
(morph_and_lookup_word pretty_print sagd)
;Test with two suffixes, expect one parse:
(morph_and_lookup_word pretty_print sagdv)
;Test with an irregular (blocking) form.  First test should fail, because
; the form we're parsing is blocked:
(morph_and_lookup_word pretty_print sid)
;Next test should succeed (it is the blocking form), one parse:
(morph_and_lookup_word pretty_print sau)

;Revise the evidential so its features intersect those of the pres tns suffix:
(load_morpher_rule message
  <rz_rule nm evidential
		   str morphophonemic
		   rz_f (evidential (witnessed) tense (pres))
		   gl WIT
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (v table3))>
					 >
					)
	>
)
;Test with both suffixes, expect one parse:
(morph_and_lookup_word pretty_print sagzv)

;Turn off tracing:
(trace_morpher_rule message (false false))
(trace_morpher_templates message (false false))
(trace_blocking message false)
(trace_lexical_lookup message false)
;...remove the rules (msg 6506):
(remove_morpher_rule message ed_suffix)
(remove_morpher_rule message s_suffix)
(remove_morpher_rule message evidential)
;...and the templates:
(set_stratum message <stratum_setting nm morphophonemic type templates value ()>)

;And remove the natural classes (msg 6542):
(remove_nat_class message null)
(remove_nat_class message C)
(remove_nat_class message V)
(remove_nat_class message alv_stop)
(remove_nat_class message voiceless_c)
(remove_nat_class message labiodental)
(remove_nat_class message voiced)
(remove_nat_class message strident)
