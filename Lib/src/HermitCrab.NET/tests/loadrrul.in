;Test loading of REALIZATIONAL RULES
;First load some nat classes (msg 6541):
(load_nat_class message <nat_class name null features ()>)
(load_nat_class message <nat_class name C features (cons +)>)
(load_nat_class message <nat_class name V features (voc +)>)
(load_nat_class message <nat_class name alv_stop features (cons + strident - del_rel - poa alveolar)>)
(load_nat_class message <nat_class name voiceless_c features (cons + vd -)>)
(load_nat_class message <nat_class name labiodental features (cons + poa labiodental)>)
(load_nat_class message <nat_class name voiced features (vd +)>)
(load_nat_class message <nat_class name voiceless features (vd -)>)
(load_nat_class message <nat_class name strident features (cons + strident +)>)
(load_nat_class message <nat_class name d features (cons + strident - del_rel - poa alveolar vd +)>)
;Load two simple realizational rules (msg 6505, twice).
; These are similar to the "ordinary" mrules in LOADMRUL.ARI.
; English -ed suffix:
(load_morpher_rule message
  <rz_rule nm ed_suffix
		   str morphophonemic
		   rz_f (tense (past))
		   gl PAST
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class alv_stop>)
									  )
						  >
					   out <m_rhs p_out (1 2 (d table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>
										<simp_cntxt class voiceless_c>
									   )
									  )
						  >
					   out <m_rhs p_out (1 (t table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 <simp_cntxt class d>)>
					 >
					)
  >
)
;English -s suffix:
(load_morpher_rule message
  <rz_rule nm s_suffix
		   str morphophonemic
		   rz_f (pers (3) tense (pres))
		   gl 3SG
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class labiodental>)
									  )
						  >
					   out <m_rhs p_out (1 (2 <simp_cntxt class voiced>) (z table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>
										<simp_cntxt class strident>
									   )
									  )
						  >
					   out <m_rhs p_out (1 (z table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>)
									   (<simp_cntxt class voiceless_c>)
									  )
						  >
					   out <m_rhs p_out (1 2 (s table3))>
					 >
					 <msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (z table3))>
					 >
					)
	>
)
;The above two rules will belong to a single slot; the following rule will
; belong to a different slot:
(load_morpher_rule message
  <rz_rule nm evidential
		   str morphophonemic
		   rz_f (evidential (witnessed))
		   gl WIT
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 (v table3))>
					 >
					)
	>
)

;Now define an inflectional template for the above rules:
(set_stratum message <stratum_setting nm morphophonemic type templates
					  value (<affix_template nm verb r_pos (V) slots ((s_suffix ed_suffix) (evidential))>)>)
;Check it (note that the order of s_suffix and ed_suffix in the slots is
; the opposite of the order in which they were loaded):
(show_stratum pretty_print morphophonemic)
;We also show it as a Prolog structure, to see whether both slots
; are optional (they should be):
.data_world(_, 'Phonology Test Language'),
 recorded(strata, Strata, _),
 member(Stratum, Strata),
 arg(1, Stratum, morphophonemic),
 arg(8, Stratum, Templates),
 Templates = [Template | _],
 writeln(Template).
;Now modify the -ed rule by removing its realizational features,
; so its slot becomes obligatory (also simplify its allomorphy,
; but that's irrelevant):
(load_morpher_rule message
  <rz_rule nm ed_suffix
		   str morphophonemic
		   gl PAST
		   subrules (<msub
					   in <m_lhs pseq ((<opt_seq min 1 max -1 seq (<simp_cntxt class null>)>))>
					   out <m_rhs p_out (1 <simp_cntxt class d>)>
					 >
					)
  >
)
;Show the template again--the first slot should now be obligatory:
.recorded(strata, Strata, _),
 member(Stratum, Strata),
 arg(1, Stratum, morphophonemic),
 arg(8, Stratum, Templates),
 Templates = [Template | _],
 writeln(Template).

;Since we haven't assigned any affixes to the allophonic stratum,
; we should get the default 'templates' there, namely '[]':
(show_stratum pretty_print allophonic)

;Now we'll remove the rules (msg 6506):
(remove_morpher_rule message ed_suffix)
(remove_morpher_rule message s_suffix)
(remove_morpher_rule message evidential)
;Verify that they're gone:
(show_stratum pretty_print morphophonemic)

;And remove the natural classes (msg 6542):
(remove_nat_class message null)
(remove_nat_class message C)
(remove_nat_class message V)
(remove_nat_class message alv_stop)
(remove_nat_class message voiceless_c)
(remove_nat_class message labiodental)
(remove_nat_class message voiced)
(remove_nat_class message strident)
