; This file was created by <T. Bailey> using TECkitMappingEditorU.exe v3.0.1.0 on 8/21/2008.
;   Conversion Type = Legacy_to_from_Unicode
;   Left-hand side font = AkrutiOriSarala-99;27.75
;   Right-hand side font = Kalinga;21.75
;   Main Window Position = 0,0,651,884
;   Left-hand side Character Map Window Position = 650,0,601,442
;   Right-hand side Character Map Window Position = 650,442,699,442

EncodingName            "AkrutiOriSarala-99"
DescriptiveName         "Oriya Akruti_Sarala_99"
Version                 "0.95"
Contact                 "mailto:Troy_Bailey@SIL.org"
RegistrationAuthority   "SIL Inc."
RegistrationName        "Oriya-AkrutiOriSarala-99"
Copyright               "© 2008 GNU General Public License"

LHSFlags                ()
RHSFlags                ()

; =============================================================================
;  OVERVIEW
; =============================================================================
; Thanks to Jonathan Kew for ideas, and for his "Annapurna.map", included
; in the SIL Converters installation.  Also, thanks to Vijay Kumar of NLCI
; for ideas found in his Winscript-to-Unicode mapping, also found in the
; SIL Converters installation.  Any mistakes here, are of course mine - T.B.
;
; PASS 1: PRE-PROCESSING OF FONT
; PASS 2: LINGUISTIC SEGMENTATION
; PASS 3: REARRANGE SYLLABLE INTO UNICODE ORDER
; PASS 4: MAP BYTES  TO UNICODE
;
; TODOS
; There are unicode chars for rra, rha ... are we mapping the nukta's to this?
; To catch some spelling mistakes we cd add a reordering rule C Matra Half_C > C Half_C Matra
; On the re-ordering, what if cbindu FOLLOWs reph on the Cons? How do we know in which order they'd appear?
; Should we add a second rule for this?
;
; REVERSABILITY ISSUES - things linguistically correct, but may not be acceptable to readers
; 0xF4 - alternate form of short i (harsiyo) renders nicely in Unicode, but doesn't make it back alive
; 0xB8 (ma+bha) fails round-trip b/c half "bha" and half "ba" share the same form
; 0xB8 <>  0x63 0xFE 0x62  ; ma+bha
; 0xF4 <>  oriya_vowel_sign_i ; talava i - secondary form, non-reversible
; Also, on return, diacritics may stack up onto each other until Unicode spacers implemented
;
; =============================================================================



; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; PASS 1 - PRE-PROCESSING OF FONT
;
;   => SPLIT MORPHED VOWEL+CONSONANT FORMS
;   => REGULARIZE DIACRITICS (E.G., SPACE, POSITIONAL VARIANTS)
;   => DUMMY CHARS FOR ZWJ & ZWNJ
;
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

pass(Byte)
class[aksharable] = ( 0x86 0x88 0x98 0xA3 0xD1 .. 0xFB 0xFD 0xFE 0xFF) ; signals anything that can attach to a base form (matras, cbindu, secondary forms, etc. - basically any phonetic symbol except full forms or full size conjuncts)
0x5F ([aksharable]*)=stuff  0xF5 <>  0x60 @stuff  ; pha (some typists mistakenly use the anusvar symbol following unasp. "pa")
0xF3    <>    0xF2 0xF1 ; imatra+cbindu
0xEE    <>    0xE2 0xEA ; ra+u
0xEF    <>    0xE2 0xEC ; ra+uu
0xFE    <>    0xFE 0xA5 ; add a ZWNJ to preserve overt virama


; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; PASS 2: LINGUISTIC SEGMENTATION
;   => DECOMPOSE CONJUNCT FORMS
;   => MAP HALF FORMS (PHALA) TO FULL FORM
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

pass(Byte)

; DECOMPOSE CONJUNCTS
0xE1    <>   0x63 0xFE 0x5A ; ma+ta
0x6C    <>  0x4B 0xFE 0x68  ; ka+ssa
0x6D    <>  0x52 0xFE 0x54  ; ja+nya
0x6E    <>  0x5C 0xFE 0x62  ; da+bha
0x6F    <>  0x4B 0xFE 0x55  ; ka+tta
0x70    <>  0x4B 0xFE 0x55 0xFE 0x65    ; k+tta+ra
0x71    <>  0x4B 0xFE 0x5A  ; ka+ta
0x72    <>  0x4B 0xFE 0x69  ; ka+sa
0x74    <>  0x4D 0xFE 0x50  ; ga+ca
0x75    <>  0x4F 0xFE 0x4B  ; nga+ka
0x76    <>  0x4F 0xFE 0x4C  ; nga+kha
0x77    <>  0x4F 0xFE 0x4D  ; nga+ga
0x78    <>  0x4F 0xFE 0x4E  ; nga+gha
0x79    <>  0x50 0xFE 0x50  ; ca+ca
0x80    <>  0x50 0xFE 0x51  ; ca+cha
0x7B    <>  0x52 0xFE 0x52  ; ja+ja
0x7C    <>  0x52 0xFE 0x53  ; ja+jha
0x7D    <>  0x5A 0xFE 0x4B  ; ta+ka
0x81    <>  0x54 0xFE 0x50  ; nya+ca
0x82    <>  0x54 0xFE 0x51  ; nya+cha
0x83    <>  0x54 0xFE 0x53  ; nya+jha
0x84    <>  0x4B 0xFE 0x68 0xFE 0x4F  ; nga+kSa
0x85    <>  0x55 0xFE 0x55  ; tta+tta
0x87    <>  0x57 0xFE 0x57  ; dda+dda
0x89    <>  0x59 0xFE 0x59  ; nna+nna
0x8A    <>  0x59 0xFE 0x57  ; nna+dda
0x8B    <>  0x59 0xFE 0x58  ; nna+ddha
0x8C    <>  0x59 0xFE 0x56  ; nna+ttha
0x8D    <>  0x59 0xFE 0x55  ; nna+tta
0x8F    <>  0x55 0xFE 0x55  ; tta+tta
0x99    <>  0x5A 0xFE 0x5D   ; ta+ma
0x9A    <>  0x5A 0xFE 0x5F   ; ta+pa
0x9B    <>  0x5A 0xFE 0x69   ; ta+sa
0x9C    <>  0x69 0xFE 0x5A 0xFE 0x5E  ; sa+ta+na
0x9D    <>  0x5A 0xFE 0x5B   ; ta+tha
0x9F    <>  0x5C 0xFE 0x5C   ; da+da
0xA1    <>  0x57 0xFE 0x58  ; da+dha
0xA2    <>  0x57 0xFE 0x4E  ; da+gha    ; check
0xA6    <>  0x5E 0xFE 0x5C  ; na+da
0xA7    <>  0x5E 0xFE 0x5D  ; na+dha
0xAA    <>  0x5E 0xFE 0x5A 0xFE 0x65  ;na+ta+ra
0xAB    <>  0x5E 0xFE 0x5A  ; na+ta
0xAC    <>  0x54 0xFE 0x52  ; na+ja
0xAE    <>  0x5F 0xFE 0x5F  ; pa+pa
0xAF    <>  0x5F 0xFE 0x5A  ; pa+ta
0xB0    <>  0x5F 0xFE 0x69  ; pa+sa
0xB1    <>  0x61 0xFE 0x5C  ; ba+da
0xB2    <>  0x61 0xFE 0x5D  ; ba+dha
0xB4    <>  0x63 0xFE 0x61  ; ma+ba
0xB5    <>  0x5D 0xFE 0x5F  ; ma+pa
0xB6    <>  0x5D 0xFE 0x60  ; ma+pha
0xB7    <>  0x55 0xFE 0x62  ; tta+bha
0xB8    <>  0x63 0xFE 0x62  ; ma+bha
0xB9    <>  0x63 0xFE 0x62  ; ma+ma
0xBA    <>  0x66 0xFE 0x4B  ; la+ka
0xBB    <>  0x66 0xFE 0x4D  ; la+ga
0xBC    <>  0x67 0xFE 0x5B  ; sha+tha
0xBD    <>  0x67 0xFE 0x50  ; sha+ca
0xBE    <>  0x68 0xFE 0x59  ; ssa+nna
0xBF    <>  0x67 0xFE 0x5F  ; ssa+pa
0xC0    <>  0x67 0xFE 0x60  ; ssa+pha
0xC1    <>  0x67 0xFE 0x55  ; ssa+tta
0xC2    <>  0x67 0xFE 0x5B  ; ssa+tha
0xC3    <>  0x67 0xFE 0x4B  ; ssa+ka
0xC4    <>  0x69 0xFE 0x4B  ; sa+ka
0xC5    <>  0x69 0xFE 0x4C  ; sa+kha
0xC6    <>  0x69 0xFE 0x5F  ; sa+pa
0xC7    <>  0x69 0xFE 0x60  ; sa+pha
0xC8    <>  0x69 0xFE 0x5A 0xFE 0x65    ; sa+ta+ra
0xC9    <>  0x69 0xFE 0x5A  ; sa+ta
0xCA    <>  0x69 0xFE 0x61  ; sa+ba
0xCB    <>  0x6B 0xFE 0x4B  ; lla+ka
0xCC    <>  0x6B 0xFE 0x5F  ; lla+pa
0xCD    <>  0x6B 0xFE 0x60  ; lla+pha
0xCF    <>  0x6B 0xFE 0x6B  ; lla+lla
0xD0    <>  0x4B 0xFE 0x68 0xFE 0x59    ; ka+ssa+nna


; HALF FORM CONSONANTS => VIRAMA + FULL FORM

134	<>     0xFE 0x6A  ; eventually goes to -> oriya_letter_ha
136	<>     0xFE 0x4B  ; eventually goes to -> oriya_letter_ka
144	<>     0xFE 0x54  ; eventually goes to -> oriya_letter_nya
152	<>     0xFE 0x5A  ; eventually goes to -> oriya_letter_ta
163	<>     0xFE 0x63  ; eventually goes to -> oriya_letter_ma
209	<>     0xFE 0x4B  ; eventually goes to -> oriya_letter_ka
210	<>     0xFE 0x4C  ; eventually goes to -> oriya_letter_kha
211	<>     0xFE 0x4D  ; eventually goes to -> oriya_letter_ga
212	<>     0xFE 0x52  ; eventually goes to -> oriya_letter_ja
213	<>     0xFE 0x53  ; eventually goes to -> oriya_letter_jha
214	<>     0xFE 0x55  ; eventually goes to -> oriya_letter_tta
215	<>     0xFE 0x56  ; eventually goes to -> oriya_letter_ttha
216	<>     0xFE 0x57  ; eventually goes to -> oriya_letter_dda
217	<>     0xFE 0x59  ; eventually goes to -> oriya_letter_nna
218	<>     0xFE 0x5B  ; eventually goes to -> oriya_letter_tha
219	<>     0xFE 0x5D  ; eventually goes to -> oriya_letter_dha
220	<>     0xFE 0x5E  ; eventually goes to -> oriya_letter_na
221	<>     0xFE 0x5F  ; eventually goes to -> oriya_letter_pa
222	<>     0xFE 0x60  ; eventually goes to -> oriya_letter_pha
223    <>     0xFE 0x96  ; virama+150 is a va placeholder
224	<>     0xFE 0x63  ; eventually goes to -> oriya_letter_ma  ; alternate to 163?
226	<>     0xFE 0x65  ; eventually goes to -> oriya_letter_ra
227	<>     0xFE 0x65  ; eventually goes to -> oriya_letter_ra
228	<>     0xFE 0x66  ; eventually goes to -> oriya_letter_la
229	<>     0xFE 0x62  ; eventually goes to -> oriya_letter_bha
230	<>     0xFE 0x6B  ; eventually goes to -> oriya_letter_lla
231	<>     0xFE 0x61  ; eventually goes to -> oriya_letter_ba
232	<>     0xFE 0x69  ; eventually goes to -> oriya_letter_sa
253    <>     0xFE 0x64  ; eventually goes to -> oriya_letter_yya ; from ya-phala (0xFD)





; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; PASS 3: REARRANGE SYLLABLE INTO UNICODE ORDER
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

pass(Byte)

class[vaTemp] = (0x96)   ; va has no full form, so using hyphen (below) as "dummy"
class[ekar] = (0xF9)
class[nukta] = (0xFF)
class[virama] = (0xFE)
class[reph] = (0xF0 )
class[cbindu] = (0xF1)
class[zwj] = (0xA8)
class[zwnj] = (0xA5)


class[cons] = ( [vaTemp]  0x6A 0x4B 0x4B 0x54 0x5A 0x63 0x4C 0x4D 0x4E 0x52 0x53 0x55 0x56 0x57 0x59 0x5B 0x5C 0x5D \
	 0x5E 0x5F 0x60 0x61 0x63 0x65 0x65 0x66 0x62 0x6B 0x61 0x69 0x4F 0x50 0x51 0x58 0x5C 0x64 0x67 0x68 0xFC ) ; contains all full-form consonants
class[vowel] = ( 0x40 0x41 0x42 0x43 0x44 0x45 0x46 0x47 0x48 0x49 0x4A 0xE9 0xEA 0xEB 0xEC 0xED 0xFA 0xF4 ) ; excluding ekar & "back_vowels"
class[back_vowel] = (0xFB 0xF7 0xF8 )   ; second of the pairs in o, ai, au


; (EKAR) CONS (CBINDU) (REPH) (VOWEL) <> @REPH @CONS @EKAR @VOWEL @CBINDU
;
[ekar]?=EKAR  (([cons] [nukta]? [virama] [zwj]? )? [cons] [nukta]?)=CONS [cbindu]?=CBINDU [reph]?=REPH ([vowel]|[back_vowel])?=VOWEL \
	<> @REPH @CONS @EKAR @VOWEL @CBINDU




; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; PASS 4: MAP BYTES TO UNICODE
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

pass(Byte_Unicode)

ByteClass[CTL] = ( 0 .. 31 127 )
UniClass[CTL]  = ( 0 .. 31 127 )
[CTL] <> [CTL]

0xFE 0xA5 <>  oriya_sign_virama zero_width_non_joiner       ; overt halant - ZWNJ
0xFE 0xA8 <>  oriya_sign_virama zero_width_joiner           ; alternate forms - ZWJ

0x96 <> oriya_letter_va     ; hyphen serving as "dummy" va

0x40 0xFB	<>	oriya_letter_aa
0xF9 0xFB	<>	oriya_vowel_sign_o
0xF9 0xF7	<>	oriya_vowel_sign_ai
0xF9 0xF8	<>	oriya_vowel_sign_au

0x20	<>	space
0x21	<>	exclamation_mark
0x22	<>	left_single_quotation_mark
0x23   <>     oriya_isshar
0x24	<>	oriya_sign_avagraha
0x25	<>	percent_sign
0x26   <>     oriya_letter_vocalic_l
0x27	<>	right_single_quotation_mark
0x28	<>	left_parenthesis
0x29	<>	right_parenthesis
0x2A	<>	asterisk
0x2B	<>	plus_sign
0x2C	<>	comma
0x2D	<>	hyphen_minus
0x2E	<>	full_stop
0x2F	<>	solidus
0x30	<>	oriya_digit_zero
0x31	<>	oriya_digit_one
0x32	<>	oriya_digit_two
0x33	<>	oriya_digit_three
0x34	<>	oriya_digit_four
0x35	<>	oriya_digit_five
0x36	<>	oriya_digit_six
0x37	<>	oriya_digit_seven
0x38	<>	oriya_digit_eight
0x39	<>	oriya_digit_nine
0x3A   <>     colon
0x3B	<>	semicolon
0x3C	<>     left_square_bracket
0x3D	<>	equals_sign
0x3E	<>	right_square_bracket
0x3F	<>	question_mark
0x40	<>	oriya_letter_a
0x41	<>	oriya_letter_i
0x42	<>	oriya_letter_ii
0x43	<>	oriya_letter_u
0x44	<>	oriya_letter_uu
0x45	<>	oriya_letter_vocalic_r
0x46	<>	oriya_letter_vocalic_rr
0x47	<>	oriya_letter_e
0x48   <>     oriya_letter_ai
0x49   <>     oriya_letter_o
0x4A   <>     oriya_letter_au
0x4B	<>	oriya_letter_ka
0x4C	<>	oriya_letter_kha
0x4D	<>	oriya_letter_ga
0x4E	<>	oriya_letter_gha
0x4F	<>	oriya_letter_nga
0x50	<>	oriya_letter_ca
0x51	<>	oriya_letter_cha
0x52	<>	oriya_letter_ja
0x53	<>	oriya_letter_jha
0x54	<>	oriya_letter_nya
0x55	<>	oriya_letter_tta
0x56	<>	oriya_letter_ttha
0x57	<>	oriya_letter_dda
0x58	<>	oriya_letter_ddha
0x59	<>	oriya_letter_nna
0x5A	<>	oriya_letter_ta
0x5B	<>	oriya_letter_tha
0x5C	<>	oriya_letter_da
0x5D	<>	oriya_letter_dha
0x5E	<>	oriya_letter_na
0x5F	<>	oriya_letter_pa
0x60	<>	oriya_letter_pha
0x61	<>	oriya_letter_ba
0x62	<>	oriya_letter_bha
0x63	<>	oriya_letter_ma
0x64	<>     oriya_letter_yya
0x65	<>	oriya_letter_ra
0x66	<>	oriya_letter_la
0x67	<>	oriya_letter_sha
0x68	<>	oriya_letter_ssa
0x69	<>	oriya_letter_sa
0x6A	<>	oriya_letter_ha
0x6B	<>	oriya_letter_lla
0x7E	<>	oriya_letter_ya
0x91   <>      left_single_quotation_mark
0x92   <>      right_single_quotation_mark
0x93   <>      left_double_quotation_mark
0x94   <>      right_double_quotation_mark
0x96   <>      en_dash
0x97   <>      em_dash
0xA5   <>      division_sign
0xA8   <>      multiplication_sign
;0xDF   <>       oriya_letter_va      ;   (font has no full va)
0xE9	<>  	  oriya_vowel_sign_vocalic_r
0xEA	<>	  oriya_vowel_sign_u
0xEB	<>	  oriya_vowel_sign_u
0xEC	<>	  oriya_vowel_sign_uu
0xED	<>	  oriya_vowel_sign_uu
0xF0	<>	  oriya_letter_ra oriya_sign_virama	;  reph / ra-phala
0xF1   <>      oriya_sign_candrabindu
0xF2   <>      oriya_vowel_sign_i
0xF4   <>      oriya_vowel_sign_i                ; talava i - secondary form, non-reversible
0xF5   <>      oriya_sign_anusvara
0xF6   <>      devanagari_danda
;0xF8   <>      oriya_ai_length_mark
;0xF9   <>      oriya_au_length_mark             ; test these
0xF9   <>      oriya_vowel_sign_e
0xFA   <>      oriya_vowel_sign_ii
0xFB   <>      oriya_vowel_sign_aa
0xFC   <>      oriya_sign_visarga
0xFE   <>      oriya_sign_virama
0xFF   <>      oriya_sign_nukta
