; This file was edited using TECkitMappingEditorU.exe v1.0.0.1 on 9/21/2006.
;   Conversion Type = Legacy_to_from_Unicode
;   Left-hand side font = Shusha;12
;   Right-hand side font = Arial Unicode MS;14.25
;   Main Window Position = 0,0,652,703
;   Code Point Window Position = 650,0,401,703

;Shusha.map
;Copyright (c) 2005 SIL International.

; Mapping table for Shusha font encoding
; based on Jonathan_Kew@sil.org's SIL-ANNAPURNA_05-2002 map:

EncodingName			"Shusha"
DescriptiveName		    "Shusha Devanagari"
Version			        "1.0"
Contact			        "mailto:bob_eaton@sall.com"
RegistrationAuthority	"SIL International"
RegistrationName		"Shusha"

RHSFlags			(ExpectsNFD)

;
; First define a bunch of things that we'll use below
Define IKAR		105
Define NUKTA	44
Define REPH     45
Define HALANT   92
Define VERTBAR  97

; In Shusha there are two kinds of half-characters: those that also have full forms, and
; those that only have half-forms for which a full form is made by adding a vertical
; bar. The former, we need to be able to convert to Unicode because they could occur in texts,
; but we won't ever need to reverse those (why? why not just go to the full form?)

; Half consonants that only have half-forms in the Shusha font (use VERTBAR to get full form)
;                    bh- gh- ny- jh- N- dh- sh- Sh- b- c- g-  j-  l-  m-  n-  th- s-  w-  y-
Define  HConlyHForms 66  71  72  74  78 81  83  89  98 99 103 106 108 109 110 113 115 118 121

; Half consonants that only have half-forms in the Shusha font (use VERTBAR to get full form)
; AND require special processing (since they involve multiple Unicode characters.
;                          tt- shr- -r(low) ksh-
Define  HConlySpecialForms 60  69   96      120

; Half consonants that also have corresponding full forms
;                    kh- t- h- k- ph- p-
Define  HCalsoFForms 35  37 42 64 70  80

; The full forms corresponding to HCalsoFForms
;                    kha ta  ha  ka  pha pa
Define  FCalsoHForms 75  116 104 107 102 112


; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; Pass 1: take embedded rephs out of the middle
;
; In some text, I saw a Shusha reph coming between a half consonant and it's vertical bar
; (e.g. ikar t- *reph* aakar). This is probably a typing error, since it looks nicest to begin
; have the order: "ikar t- aakar reph". Nevertheless, if we see this order, then "fix" the
; order here (i.e. to move the reph to the end where it belongs, so it'll match the order
; expected by pass 3), but don't make this reversable (since we don't ever want to go back to
; the form we're handling here)
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
pass(Byte)
class [HC] = (HConlyHForms HConlySpecialForms HCalsoFForms)

[HC]=cons REPH=reph VERTBAR=aakar > @cons @aakar @reph

; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; Pass 2: remove redundant forms
;
; shusha has some duplicate glyphs... We're always going to use only one of the forms
; also handle the case where the user typed a half-cononant followed by a vertical bar
; for a letter which has a full form in the font (make it always be the full form)
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
pass(Byte)

; turn any half-consonant plus a following vertical bar into the corresponding
; full consonant form (unidirectional conversion)
class [HC] = (HCalsoFForms)
class [FC] = (FCalsoHForms)
[HC] VERTBAR > [FC]

; some punctuation seems to be in the font twice. We'll normalize to one form (also unidirectional)
88  >   83  ; use only the 83 sh-
207 >   83  ; use only the 83 sh-
209 >   178 ; use only the 178 exclamation mark
184 >   202 ; use only the 130 comma (not sure if these are the same)
130 >   202 ; use only the 130 comma (not sure if these are the same)
216 >   136 ; circumflex

; build any full syllable forms from the pieces
159 >   105 106 97  ; full syllable ji (i.e. d159) is really ugly

; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; Pass 3: rearrange syllable into Unicode order
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; Byte order:
;	ikar?
;	(cons nukta? virama)*
;	cons nukta?
;	vowel?
;	reph?
;	nasal?
;
; Unicode order:
;	reph?
;	(cons nukta? virama)*
;	cons nukta?
;	(vowel | ikar)?
;	nasal?

pass(Byte)
; Half-consonant forms
class [HC] = ( HConlyHForms HConlySpecialForms HCalsoFForms )

; Full-consonant forms
;              om Nga cha Da kha La Ta Dha da  pha ha  ka  pa  ra  ta  Tha
class [FC] = ( 33 61  67  68 75  76 84 90  100 102 104 107 112 114 116 122 )


; Stack consonant forms
;            gya hya hri TTa kta dya dwa dda ddha TTha tra hra kra phra Xa  fa  nna
class [SC] = (38  40  41  43  62  86  87  95  119  123  126 156 203 205  211 212 217)

; Dependent vowel forms
; though ikar is a dependent vowel, don't include it here, because it never comes *after* the
; consonant we're trying to reorder below
;                   visarga ii ai ri uu aa e   u   o   au
class [DepVowel] = (58      73 79 82 85 97 111 117 220 221)

; Nasal forms
; anusvar chandrabindu
class [N] =(77 208)

; 'C' can go immediately before vowels (thus, full-consonants and full-stacks)
class[C] = ([FC] [SC])

; optional post-consonantal things (nukta and low rs)
class[PC] = (NUKTA 47 96)

; The tricky part here is 'cons': It should include all full-consonants and other consonant clusters.
IKAR?=ikar ((([FC] NUKTA? HALANT)|([HC] NUKTA?))* ([C]|[HC] VERTBAR) [PC]?)=cons [DepVowel]?=vwl REPH?=reph [N]?=nas \
		<> @reph @cons @ikar @vwl @nas


; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; Pass 4: map byte codes to Unicodes
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; This pass deals with "basic" byte codes; it never sees the "presentation form"
; byte codes for half forms/conjuncts/positional variants

pass(Byte_Unicode)

ByteClass[CTL]  = ( 0 .. 31 )
UniClass[CTL]   = ( 0 .. 31 )

ByteClass[HConly]   = ( HConlyHForms )

UniClass[FConly]    = ( devanagari_letter_bha   devanagari_letter_gha   devanagari_letter_nya   \
						devanagari_letter_jha   devanagari_letter_nna   devanagari_letter_dha   \
						devanagari_letter_sha   devanagari_letter_ssa   devanagari_letter_ba    \
						devanagari_letter_ca    devanagari_letter_ga    devanagari_letter_ja    \
						devanagari_letter_la    devanagari_letter_ma    devanagari_letter_na    \
						devanagari_letter_tha   devanagari_letter_sa    devanagari_letter_va    \
						devanagari_letter_ya    )

ByteClass[HCalso]   = ( HCalsoFForms )

UniClass[FCalso]    = ( devanagari_letter_kha   devanagari_letter_ta    devanagari_letter_ha    \
						devanagari_letter_ka    devanagari_letter_pha   devanagari_letter_pa    )

; first do the easy ones
[CTL]   <>  [CTL]

; all half-chars followed by the vertical bar are turned into full chars
[HConly] VERTBAR	<>	[FConly]

; all the rest get the corresponding full char + virama + ZWJ
[HConly]            <	[FConly] devanagari_sign_virama 0x200d
[HConly]            <>	[FConly] devanagari_sign_virama

; also have to handle the remain half-consonants that have full forms (but which weren't
; converted in pass one to the full forms (i.e. they are bonefide half forms only with
; no trailing vertical bar.
[HCalso]            <>  [FCalso] devanagari_sign_virama 0x200d

32	<>	space
33	<>	devanagari_om
34	<>	quotation_mark

; 35	<>	kh- (i.e. covered by HConly and HCalso rule above, and hereafter)

36	<>	devanagari_letter_ra devanagari_vowel_sign_uu

; 37	<>	t-

38	<>	devanagari_letter_ja devanagari_sign_virama devanagari_letter_nya

39	<>	apostrophe

40	<>	devanagari_letter_ha devanagari_sign_virama devanagari_letter_ya
41	<>	devanagari_letter_ha devanagari_vowel_sign_vocalic_r

; 42	<>	h-

43	<>	devanagari_letter_tta devanagari_sign_virama devanagari_letter_tta
44	<>	devanagari_sign_nukta
45	<>	devanagari_letter_ra devanagari_sign_virama	; reph
46	<>	devanagari_danda
47	<>	devanagari_sign_virama devanagari_letter_ra

48	<>	digit_zero
49	<>	digit_one
50	<>	digit_two
51	<>	digit_three
52	<>	digit_four
53	<>	digit_five
54	<>	digit_six
55	<>	digit_seven
56	<>	digit_eight
57	<>	digit_nine

58	<>	devanagari_sign_visarga
59	<>	DEVANAGARI_LETTER_VOCALIC_L
60	<>	devanagari_letter_ta devanagari_sign_virama devanagari_letter_ta devanagari_sign_virama 0x200d
60	VERTBAR <>	devanagari_letter_ta devanagari_sign_virama devanagari_letter_ta
61	<>	devanagari_letter_nga
; original 62	<>	devanagari_letter_ka devanagari_sign_virama devanagari_letter_ta
62	<	devanagari_letter_ka devanagari_sign_virama devanagari_letter_ta
62	<>	devanagari_letter_ka devanagari_sign_virama 0x200d devanagari_letter_ta
63	<>	devanagari_letter_vocalic_r

; 64	<>	k-

65	        <>	devanagari_letter_a
65 VERTBAR  <>	devanagari_letter_aa
65 220      <>	devanagari_letter_o
65 221      <>  devanagari_letter_au

; 66	<>	bh-

67	<>	devanagari_letter_cha
68	<>	devanagari_letter_dda
69	<>	devanagari_letter_sha devanagari_sign_virama devanagari_letter_ra devanagari_sign_virama 0x200d
69 VERTBAR	<>	devanagari_letter_sha devanagari_sign_virama devanagari_letter_ra

; 70	<>	ph-
; 71	<>	gh-
; 72	<>	ny-

73	<>	devanagari_vowel_sign_ii

; 74	<>	jh-

75	<>	devanagari_letter_kha
76	<>	devanagari_letter_lla
77	<>	devanagari_sign_anusvara

; 78	<>	N-

79  <>  devanagari_vowel_sign_ai

; 80    <>  p-
; 81    <>  dh-

82  <>  devanagari_vowel_sign_vocalic_r

; 83	<>	sh-

84  <>  devanagari_letter_tta
85	<>	devanagari_vowel_sign_uu
86  <>  devanagari_letter_da devanagari_sign_virama devanagari_letter_ya
87  <>  devanagari_letter_da devanagari_sign_virama devanagari_letter_va

; 88    <>  sh2-
; 89	<>	SH-

90  <>  devanagari_letter_ddha
91	<>	devanagari_letter_i
92	<	devanagari_sign_virama 0x200c   ; explicit halant
92	<>	devanagari_sign_virama          ; to handle some FC + FC stacks that don't exist in Shusha e.g. dma
93	<>	devanagari_letter_u
94	<>	devanagari_vowel_sign_candra_e
95	<>	devanagari_letter_da devanagari_sign_virama devanagari_letter_da
96	<>	devanagari_sign_virama devanagari_letter_ra

97  <>  devanagari_vowel_sign_aa

; 98  <>  b-
; 99  <>  c-

100 <>  devanagari_letter_da

101     <>  devanagari_letter_e
101 111 <>  devanagari_letter_ai

102 <>  devanagari_letter_pha

; 103	<>	g-

104 <>  devanagari_letter_ha
105 <>  devanagari_vowel_sign_i

; 106	<>	j-

107	<>	devanagari_letter_ka

; 108   <>  l-
; 109	<>	m-
; 110	<>	n-

111	<>	devanagari_vowel_sign_e
112 <>  devanagari_letter_pa

; 113   <>  th-

114	<>	devanagari_letter_ra

; 115   <>  s-

116	<>	devanagari_letter_ta
117 <>  devanagari_vowel_sign_u

; 118   <>  w-

119	<>	devanagari_letter_da devanagari_sign_virama devanagari_letter_dha
120 <>  devanagari_letter_ka devanagari_sign_virama devanagari_letter_ssa devanagari_sign_virama
120 VERTBAR <>  devanagari_letter_ka devanagari_sign_virama devanagari_letter_ssa

; 121	<>	y-

122 <>  devanagari_letter_ttha
123	<>	devanagari_letter_ttha devanagari_sign_virama devanagari_letter_ttha
124	<>	devanagari_sign_avagraha
125	<>	devanagari_letter_uu
126	<>	devanagari_letter_ta devanagari_sign_virama devanagari_letter_ra

; 127   <>
; 128	<>
; 129	<>

; 130 <>  handled in pass 2; technically not a comma, but is probably a mistake for a comma

; 131	<>

132 <>  DOUBLE_LOW_9_QUOTATION_MARK
133 <>	horizontal_ellipsis
134 <>  dagger
135 <>  DOUBLE_DAGGER
136	<>	MODIFIER_LETTER_CIRCUMFLEX_ACCENT
137 <>  percent_sign    ; technically a "PER MILLE SIGN", but looks like a percent sign

; 138	<>

139 <>  less_than_sign  ; technically a "SINGLE LEFT-POINTING ANGLE QUOTATION MARK", but looks like <

; 140   <>
; 141   <>
; 142   <>
; 143   <>
; 144	<>

145	<>	left_single_quotation_mark
146	<>	right_single_quotation_mark
147	<>	left_double_quotation_mark
148	<>	right_double_quotation_mark
149 <>  bullet
150	<>	en_dash
151	<>	em_dash
152	<>	tilde
153 <>  TRADE_MARK_SIGN

154 <>  devanagari_letter_ii

155	<>	greater_than_sign   ; technically this is a "SINGLE RIGHT-POINTING ANGLE QUOTATION MARK", but looks like ">"

156 <>  devanagari_letter_ha devanagari_sign_virama devanagari_letter_ra

; 157   <>
; 158	<>

; 159 <>  devanagari_letter_ja devanagari_vowel_sign_i handled in pass 2

160 <>  no_break_space

161	<>	left_curly_bracket
162	<>	cent_sign
163 <>  right_curly_bracket
164	<>	left_square_bracket
165	<>	right_square_bracket
166 <>  BROKEN_BAR
167	<>	SECTION_SIGN
168 <>  DIAERESIS
169 <>  COPYRIGHT_SIGN
170	<>	FEMININE_ORDINAL_INDICATOR ; ??? not sure what this is for (left single quote?)
171 <>  LEFT_POINTING_DOUBLE_ANGLE_QUOTATION_MARK
172	<>	NOT_SIGN
173	<>	SOFT_HYPHEN
174	<>	REGISTERED_SIGN
175	<>	MACRON
176	<>	DEGREE_SIGN
177	<>	PLUS_MINUS_SIGN
178 <>  exclamation_mark
179 <>  left_parenthesis
180	<>	right_parenthesis
181 <>  asterisk
182	<>	pilcrow_sign
183	<>	middle_dot

; 184   <>  handled in pass 2

185	<>	hyphen_minus
186 <>  devanagari_sign_nukta   ; ??? not sure if this is a nukta

187   <>  RIGHT_POINTING_DOUBLE_ANGLE_QUOTATION_MARK

188	<>	vulgar_fraction_one_quarter
189	<>	vulgar_fraction_one_half
190	<>	vulgar_fraction_three_quarters
191	<>	INVERTED_QUESTION_MARK
192 <>  solidus
193 <>  colon
194 <>  semicolon
195 <>  less_than_sign
196	<>	equals_sign
197 <>  greater_than_sign
198 <>  question_mark
199 <>  ampersand

200	>	devanagari_danda    ; seems to be the full stop in some text I saw, but doesn't much look like one...
201 <>  devanagari_letter_ra devanagari_vowel_sign_u

202 <>  comma

203 <>  devanagari_letter_ka devanagari_sign_virama devanagari_letter_ra
204	<>	devanagari_letter_ka devanagari_vowel_sign_vocalic_r
205 <>  devanagari_letter_pha devanagari_sign_virama devanagari_letter_ra
206 <>  devanagari_letter_pha devanagari_vowel_sign_vocalic_r

; 207 <>  sh-

208 <>  devanagari_sign_candrabindu

; 209 <>  handled in pass 2

210 <>  reverse_solidus

211 <>  DEVANAGARI_LETTER_KHA devanagari_sign_nukta
212	<>	DEVANAGARI_LETTER_PHA devanagari_sign_nukta

; 213	<>	not in Unicode
; 214	<>	not sure what this is in Unicode

215	<>	MULTIPLICATION_SIGN

; 216	<>	handled in pass 2

217	<>	devanagari_letter_na devanagari_sign_virama devanagari_letter_na

; 218	<>	not sure what this is in Unicode
; 219	<>

220 <>  devanagari_vowel_sign_o
221	<>	devanagari_vowel_sign_au

; 222	<>
; 223	<>
; 224	<>
; 225	<>
; 226	<>
; 227	<>
; 228	<>
; 229	<>
; 230	<>
; 231	<>
; 232	<>
; 233	<>
; 234	<>
; 235	<>
; 236	<>
; 237	<>
; 238	<>
; 239	<>
; 240	<>
; 241	<>
; 242	<>
; 243	<>
; 244	<>
; 245	<>
; 246	<>

247	<>  DIVISION_SIGN
248	<>  LATIN_CAPITAL_LETTER_O_WITH_STROKE

; 249	<>
; 250	<>
; 251	<>
; 252	<>
; 253	<>
; 254	<>
; 255	<>

; finally, if we inserted the low dot after a half character, then it will be in the wrong
; order, so re-arrange it. all half-characters will be full chars followed by halant + zwj
pass(Unicode)

devanagari_sign_virama=halant 0x200d=zwj devanagari_sign_nukta=dot <> \
	@dot @halant @zwj
