; Tibetan Transcription TECkit mapping
;  for codepage TibetanA
;
; $Log: TibA2Uni.map,v $
; Revision 1.1  2005/03/16 04:13:18  Marius1
; First revision.
;
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
EncodingName              "TIBETAN-MOD-A"
DescriptiveName           "Tibetan Modern A"
Version                   "1.0"
Contact                   "mailto:marius@doornenbal.org"
RegistrationAuthority     "SIL International"
RegistrationName          "Tibetan-Modern-A"
Copyright                 "Marius Doornenbal - 28 May 2003"
RHSFlags                   (ExpectsNFD)


; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; pass 1: Normalise Tibetan A
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
pass(Byte)
;
; There are a LOT of different 'u' markings in Tibetan Mod A.
; The classes of consonants that take them, respectively, are named after the 'u' they take.
; Ultimately, the only 'u' left will be the u81 (129)
;
class[u24] = (98 100 103 106 109 110 111 112 115 118 119 120 121 122 124 126 67 82 194)
class[u81] = (101 102 113 114)
class[u82] = (116 217)
class[u83] = (68 73 74 75 76)
class[u84] = (208 209 210 211 212 213 214)
class[u85] = (240 241 242 243 244 245 246 247 248 249 250 251 252)
class[u86] = (104 105 125 70 216)
class[u87] = (65 66 71 72 97 99 107 108 117 123 176 177 178 179 180 181 182 183 184 185 \
			  186 187 188 189 190 191)
class[u88] = (83 84 87 88 90 91 92 93 94 95 199 200 201 202 229)
class[u89] = (69 85 89 195 196 221 224 225 226 227 228)
class[u8a] = (80 81 86 192 193 194 197 198 234 235 236 237 238 239 253 254 255)
class[u8b] = (203 204 205 206 207)
class[u91] = (77 78 79)
class[u92] = (218 219 220)

; lower diacritics
class[ldia] = (44 141 143 147 152 154 156 158 36 129 130 131 132 133 134 135 136 137 138 \
			   139 145 146 140 142 144 148 153 155 157 159 150 151)

; right wing consonants
class[right] = (113 114 115 183 184)

;
; convert the u's - all to 129
 36 <> 129 / [u24] _
129 <> 129 / [u81] _
130 <> 129 / [u82] _
131 <> 129 / [u83] _
132 <> 129 / [u84] _
133 <> 129 / [u85] _
134 <> 129 / [u86] _
135 <> 129 / [u87] _
136 <> 129 / [u88] _
137 <> 129 / [u89] _
138 <> 129 / [u8a] _
139 <> 129 / [u8b] _
145 <> 129 / [u91] _
146 <> 129 / [u92] _

;
; convert the a-chung - all to 141
 44 <> 141 / [u24] _                ; x2c
 44 <> 141 / [u81] _                ; x2c
 44 <> 141 / [u82] _                ; x2c
141 <> 141 / [u83] _                ; x8d
143 <> 141 / [u84] _                ; x8f
143 <> 141 / [u85] _                ; x8f
143 <> 141 / [u86] _                ; x8f
152 <> 141 / [u87] _                ; x98
154 <> 141 / [u88] _                ; x9a
147 <> 141 / [u89] _                ; x93
156 <> 141 / [u8a] _                ; x9c
156 <> 141 / [u8b] _                ; x9c
154 <> 141 / [u91] _                ; x9a
143 <> 141 / [u92] _                ; x8f

;
; convert the combination of a-chung and u into the right byte

140 <> 140 / [u24] _                ; x8c
140 <> 140 / [u81] _                ; x8c
140 <> 140 / [u82] _                ; x8c
142 <> 140 / [u83] _                ; x8e
144 <> 140 / [u84] _                ; x90
144 <> 140 / [u85] _                ; x90
144 <> 140 / [u86] _                ; x90
144 <> 140 / [u92] _                ; x90
148 <> 140 / [u89] _                ; x94
153 <> 140 / [u87] _                ; x99
155 <> 140 / [u88] _                ; x9b
155 <> 140 / [u91] _                ; x9b
157 <> 140 / [u8a] _                ; x9d
157 <> 140 / [u8b] _                ; x9d
159  > 140                          ; M.Hosken commented 'never used as far as I can see'

; other vowel diacritics
 33 <> 33                           ; default -i-
161 <> 33 / [right] [ldia]? _       ; right -i-
 41 <> 41                           ; default nasal -i-
169 <> 41 / [right] [ldia]? _       ; right nasal -i-
 34 <> 34                           ; default -e-
162 <> 34 / [right] [ldia]? _       ; right -e-
 35 <> 35                           ; default -o-
163 <> 35 / [right] [ldia]? _       ; right -o-
 40 <> 40                           ; default nasal -o-
168 <> 40 / [right] [ldia]? _       ; right nasal -o-

; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; pass 2: Tibetan A directly to Unicode
; * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

pass(Byte_Unicode)


ByteClass[CTL] = ( 0 .. 31 127 )
UniClass[CTL]  = ( 0 .. 31 127 )

[CTL] <> [CTL]

32 <>  SPACE

; simple vowels
33 <>  TIBETAN_VOWEL_SIGN_I
; 161 <>  TIBETAN_VOWEL_SIGN_I
34 <>  TIBETAN_VOWEL_SIGN_E
; 162 <>  TIBETAN_VOWEL_SIGN_E
35 <>  TIBETAN_VOWEL_SIGN_O
; 163 <>  TIBETAN_VOWEL_SIGN_O
129 <>  TIBETAN_VOWEL_SIGN_U
; 36 <>  TIBETAN_VOWEL_SIGN_U
37 <>  TIBETAN_VOWEL_SIGN_REVERSED_I
38 <>  TIBETAN_VOWEL_SIGN_EE
39 <>  TIBETAN_VOWEL_SIGN_OO
141 <>  TIBETAN_VOWEL_SIGN_AA
; 44 <>  TIBETAN_VOWEL_SIGN_AA

; combination
140 <>  TIBETAN_VOWEL_SIGN_AA TIBETAN_VOWEL_SIGN_U

; vowels + diacritic combination
40  <>  TIBETAN_VOWEL_SIGN_O TIBETAN_SIGN_RJES_SU_NGA_RO
; 168 <>  TIBETAN_VOWEL_SIGN_O TIBETAN_SIGN_RJES_SU_NGA_RO

41  <>  TIBETAN_VOWEL_SIGN_I TIBETAN_SIGN_RJES_SU_NGA_RO
; 169 <>  TIBETAN_VOWEL_SIGN_I TIBETAN_SIGN_RJES_SU_NGA_RO

165 <>  TIBETAN_VOWEL_SIGN_REVERSED_I TIBETAN_SIGN_RJES_SU_NGA_RO
166 <>  TIBETAN_VOWEL_SIGN_EE TIBETAN_SIGN_RJES_SU_NGA_RO
167 <>  TIBETAN_VOWEL_SIGN_OO TIBETAN_SIGN_RJES_SU_NGA_RO


42 <>  TIBETAN_SIGN_RJES_SU_NGA_RO
43 <>  TIBETAN_SIGN_SNA_LDAN

45 <>  TIBETAN_MARK_INTERSYLLABIC_TSHEG
46 <>  TIBETAN_MARK_SHAD
47 <>  TIBETAN_MARK_RIN_CHEN_SPUNGS_SHAD
48 <>  TIBETAN_DIGIT_ZERO
49 <>  TIBETAN_DIGIT_ONE
50 <>  TIBETAN_DIGIT_TWO
51 <>  TIBETAN_DIGIT_THREE
52 <>  TIBETAN_DIGIT_FOUR
53 <>  TIBETAN_DIGIT_FIVE
54 <>  TIBETAN_DIGIT_SIX
55 <>  TIBETAN_DIGIT_SEVEN
56 <>  TIBETAN_DIGIT_EIGHT
57 <>  TIBETAN_DIGIT_NINE
58 <>  TIBETAN_SIGN_RNAM_BCAD
59 <>  TIBETAN_MARK_GTER_TSHEG
60 <>  TIBETAN_MARK_ANG_KHANG_GYON
61 <>  TIBETAN_MARK_BSDUS_RTAGS
62 <>  TIBETAN_MARK_ANG_KHANG_GYAS
63 <>  TIBETAN_SIGN_LCI_RTAGS
64 <>  TIBETAN_MARK_INITIAL_YIG_MGO_MDUN_MA
65 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_KA
66 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_GA
67 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_NGA
68 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_JA
69 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_NYA
70 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_TA
71 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_DA
72 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_NA
73 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_BA
74 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_MA
75 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_TSA
76 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_DZA
77 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_KA TIBETAN_SUBJOINED_LETTER_YA
78 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_GA TIBETAN_SUBJOINED_LETTER_YA
79 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_MA TIBETAN_SUBJOINED_LETTER_YA
80 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_KA
81 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_GA
82 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_NGA
83 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_CA
84 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_JA
85 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_TA
86 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_DA
87 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_PA
88 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_BA
89 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_HA
90 <>  TIBETAN_LETTER_KA TIBETAN_SUBJOINED_LETTER_LA
91 <>  TIBETAN_LETTER_GA TIBETAN_SUBJOINED_LETTER_LA
92 <>  TIBETAN_LETTER_BA TIBETAN_SUBJOINED_LETTER_LA
93 <>  TIBETAN_LETTER_ZA TIBETAN_SUBJOINED_LETTER_LA
94 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_LA
95 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_LA
96 <>  TIBETAN_MARK_CLOSING_YIG_MGO_SGAB_MA
97 <>  TIBETAN_LETTER_KA
98 <>  TIBETAN_LETTER_KHA
99 <>  TIBETAN_LETTER_GA
100 <>  TIBETAN_LETTER_NGA
101 <>  TIBETAN_LETTER_CA
102 <>  TIBETAN_LETTER_CHA
103 <>  TIBETAN_LETTER_JA
104 <>  TIBETAN_LETTER_NYA
105 <>  TIBETAN_LETTER_TA
106 <>  TIBETAN_LETTER_THA
107 <>  TIBETAN_LETTER_DA
108 <>  TIBETAN_LETTER_NA
109 <>  TIBETAN_LETTER_PA
110 <>  TIBETAN_LETTER_PHA
111 <>  TIBETAN_LETTER_BA
112 <>  TIBETAN_LETTER_MA
113 <>  TIBETAN_LETTER_TSA
114 <>  TIBETAN_LETTER_TSHA
115 <>  TIBETAN_LETTER_DZA
116 <>  TIBETAN_LETTER_WA
117 <>  TIBETAN_LETTER_ZHA
118 <>  TIBETAN_LETTER_ZA
119 <>  TIBETAN_LETTER__A
120 <>  TIBETAN_LETTER_YA
121 <>  TIBETAN_LETTER_RA
122 <>  TIBETAN_LETTER_LA
123 <>  TIBETAN_LETTER_SHA
124 <>  TIBETAN_LETTER_SA
125 <>  TIBETAN_LETTER_HA
126 <>  TIBETAN_LETTER_A

127 <> 0x007f

149 <>  TIBETAN_SYMBOL_RDO_RJE_RGYA_GRAM
150 <>  TIBETAN_MARK_NGAS_BZUNG_SGOR_RTAGS
151 <>  TIBETAN_MARK_NGAS_BZUNG_NYI_ZLA

160 <>  NO_BREAK_SPACE
164 <>  TIBETAN_SIGN_LCE_TSA_CAN

170 <>  TIBETAN_SIGN_MCHU_CAN
171 <>  TIBETAN_SIGN_NYI_ZLA_NAA_DA
172 <>  TIBETAN_SIGN_MCHU_CAN
173 <>  TIBETAN_MARK_TSA__PHRU
174 <>  TIBETAN_MARK_RIN_CHEN_SPUNGS_SHAD
175 <>  TIBETAN_MARK_SBRUL_SHAD
176 <>  TIBETAN_LETTER_KA TIBETAN_SUBJOINED_LETTER_WA
177 <>  TIBETAN_LETTER_KHA TIBETAN_SUBJOINED_LETTER_WA
178 <>  TIBETAN_LETTER_GA TIBETAN_SUBJOINED_LETTER_WA
179 <>  TIBETAN_LETTER_CA TIBETAN_SUBJOINED_LETTER_WA
180 <>  TIBETAN_LETTER_NYA TIBETAN_SUBJOINED_LETTER_WA
181 <>  TIBETAN_LETTER_TA TIBETAN_SUBJOINED_LETTER_WA
182 <>  TIBETAN_LETTER_DA TIBETAN_SUBJOINED_LETTER_WA
183 <>  TIBETAN_LETTER_TSA TIBETAN_SUBJOINED_LETTER_WA
184 <>  TIBETAN_LETTER_TSHA TIBETAN_SUBJOINED_LETTER_WA
185 <>  TIBETAN_LETTER_ZHA TIBETAN_SUBJOINED_LETTER_WA
186 <>  TIBETAN_LETTER_ZA TIBETAN_SUBJOINED_LETTER_WA
187 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_WA
188 <>  TIBETAN_LETTER_LA TIBETAN_SUBJOINED_LETTER_WA
189 <>  TIBETAN_LETTER_SHA TIBETAN_SUBJOINED_LETTER_WA
190 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_WA
191 <>  TIBETAN_LETTER_HA TIBETAN_SUBJOINED_LETTER_WA
192 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_KA
193 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_GA
194 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_NGA
195 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_NYA
196 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_TA
197 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_DA
198 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_NA
199 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_PA
200 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_BA
201 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_MA
202 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_TSA
203 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_KA TIBETAN_SUBJOINED_LETTER_YA
204 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_GA TIBETAN_SUBJOINED_LETTER_YA
205 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_PA TIBETAN_SUBJOINED_LETTER_YA
206 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_BA TIBETAN_SUBJOINED_LETTER_YA
207 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_MA TIBETAN_SUBJOINED_LETTER_YA
208 <>  TIBETAN_LETTER_KA TIBETAN_SUBJOINED_LETTER_YA
209 <>  TIBETAN_LETTER_KHA TIBETAN_SUBJOINED_LETTER_YA
210 <>  TIBETAN_LETTER_GA TIBETAN_SUBJOINED_LETTER_YA
211 <>  TIBETAN_LETTER_PA TIBETAN_SUBJOINED_LETTER_YA
212 <>  TIBETAN_LETTER_PHA TIBETAN_SUBJOINED_LETTER_YA
213 <>  TIBETAN_LETTER_BA TIBETAN_SUBJOINED_LETTER_YA
214 <>  TIBETAN_LETTER_MA TIBETAN_SUBJOINED_LETTER_YA
215 <>  TIBETAN_KU_RU_KHA
216 <>  TIBETAN_LETTER_TTA
217 <>  TIBETAN_LETTER_TTHA
218 <>  TIBETAN_LETTER_DDA
219 <>  TIBETAN_LETTER_NNA
220 <>  TIBETAN_LETTER_SSA
221 <>  TIBETAN_LETTER_KSSA
222 <>  TIBETAN_MARK_GTER_YIG_MGO_TRUNCATED_A
223 <>  TIBETAN_MARK_PALUTA
224 <>  TIBETAN_LETTER_GHA
225 <>  TIBETAN_LETTER_NGA TIBETAN_SUBJOINED_LETTER_HA
226 <>  TIBETAN_LETTER_BHA
227 <>  TIBETAN_LETTER_DZHA
228 <>  TIBETAN_LETTER_DDHA
229 <>  TIBETAN_LETTER_DA TIBETAN_SUBJOINED_LETTER_MA
230 <>  TIBETAN_LETTER_RA
231 <>  TIBETAN_SUBJOINED_LETTER_WA
231 <>  TIBETAN_SUBJOINED_LETTER_WA
231 <>  TIBETAN_SUBJOINED_LETTER_WA
232 <>  TIBETAN_SUBJOINED_LETTER_YA
232 <>  TIBETAN_SUBJOINED_LETTER_YA
233 <>  TIBETAN_SUBJOINED_LETTER_RA
233 <>  TIBETAN_SUBJOINED_LETTER_RA
233 <>  TIBETAN_SUBJOINED_LETTER_RA
233 <>  TIBETAN_SUBJOINED_LETTER_RA
234 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_KA TIBETAN_SUBJOINED_LETTER_RA
235 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_GA TIBETAN_SUBJOINED_LETTER_RA
236 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_NA TIBETAN_SUBJOINED_LETTER_RA
237 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_PA TIBETAN_SUBJOINED_LETTER_RA
238 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_BA TIBETAN_SUBJOINED_LETTER_RA
239 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_MA TIBETAN_SUBJOINED_LETTER_RA
240 <>  TIBETAN_LETTER_KA TIBETAN_SUBJOINED_LETTER_RA
241 <>  TIBETAN_LETTER_KHA TIBETAN_SUBJOINED_LETTER_RA
242 <>  TIBETAN_LETTER_GA TIBETAN_SUBJOINED_LETTER_RA
243 <>  TIBETAN_LETTER_TA TIBETAN_SUBJOINED_LETTER_RA
244 <>  TIBETAN_LETTER_THA TIBETAN_SUBJOINED_LETTER_RA
245 <>  TIBETAN_LETTER_DA TIBETAN_SUBJOINED_LETTER_RA
246 <>  TIBETAN_LETTER_PA TIBETAN_SUBJOINED_LETTER_RA
247 <>  TIBETAN_LETTER_PHA TIBETAN_SUBJOINED_LETTER_RA
248 <>  TIBETAN_LETTER_BA TIBETAN_SUBJOINED_LETTER_RA
249 <>  TIBETAN_LETTER_MA TIBETAN_SUBJOINED_LETTER_RA
250 <>  TIBETAN_LETTER_SHA TIBETAN_SUBJOINED_LETTER_RA
251 <>  TIBETAN_LETTER_SA TIBETAN_SUBJOINED_LETTER_RA
252 <>  TIBETAN_LETTER_HA TIBETAN_SUBJOINED_LETTER_RA
253 <>  TIBETAN_LETTER_GA TIBETAN_SUBJOINED_LETTER_RA TIBETAN_SUBJOINED_LETTER_WA
254 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_GA TIBETAN_SUBJOINED_LETTER_WA
255 <>  TIBETAN_LETTER_RA TIBETAN_SUBJOINED_LETTER_TSA TIBETAN_SUBJOINED_LETTER_WA
